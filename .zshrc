# Created by newuser for 5.0.2

#setopt correct             # command not foundãªæ™‚ã«ãŠç¯€ä»‹ã™ã‚‹
setopt list_packed          # è£œå®Œã‚’è©°ã‚ã‚‹
setopt globdots             # ãƒ‰ãƒƒãƒˆãƒ•ã‚¡ã‚¤ãƒ«ã‚’*ã§é¸æŠžã™ã‚‹
setopt noautoremoveslash    # ãƒ‘ã‚¹ã®æœ€å¾Œã®ã‚¹ãƒ©ãƒƒã‚·ãƒ¥ã‚’å‰Šé™¤ã—ãªã„
setopt mark_dirs            # ãƒ•ã‚¡ã‚¤ãƒ«åã®å±•é–‹ã§ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã«ãƒžãƒƒãƒã—ãŸå ´åˆ æœ«å°¾ã« / ã‚’ä»˜åŠ 
setopt print_eight_bit      # æ—¥æœ¬èªžãƒ•ã‚¡ã‚¤ãƒ«åç­‰8ãƒ“ãƒƒãƒˆã‚’é€šã™
setopt extended_glob        # å¼·ã„glob
setopt no_beep
setopt INTERACTIVE_COMMENTS

### export variables
export EDITOR=vim
export GOPATH="$HOME/go"
export PATH=$PATH:$GOPATH/bin
export PATH=$HOME/bin:$PATH
export PATH=$HOME/.rbenv/bin:$PATH
which rbenv > /dev/null 2>&1 && eval "$(rbenv init -)"
# https://wiki.archlinux.org/index.php/Java_Runtime_Environment_Fonts_%28%E6%97%A5%E6%9C%AC%E8%AA%9E%29
export _JAVA_OPTIONS='-Dawt.useSystemAAFontSettings=on'
export MANPAGER="/bin/sh -c \"col -b -x | vim -c 'set buftype=nofile' -c 'set ft=man' -\""
export RUBYOPT='-W'

WORDCHARS="${WORDCHARS:s!/!!}"
REPORTTIME=3                # 3ç§’ä»¥ä¸Šã‹ã‹ã£ãŸã‚³ãƒžãƒ³ãƒ‰ã¯å®Ÿè¡Œæ™‚é–“ã‚’è¡¨ç¤ºã™ã‚‹
TIMEFMT='
total  %E
user   %U
system %S
CPU    %P
cmd    %J'

disable r

### compinit
autoload -U compinit        # è£œå®ŒãŒå¼·ããªã‚‹
compinit -u
setopt auto_param_slash     # ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªåã®è£œå®Œã§æœ«å°¾ã® / ã‚’è‡ªå‹•çš„ã«ä»˜åŠ ã—ã€æ¬¡ã®è£œå®Œã«å‚™ãˆã‚‹
setopt magic_equal_subst    # = ä»¥é™ã§ã‚‚è£œå®Œ
#  è£œå®Œã®éš›(å¤§|å°)æ–‡å­—ã‚’åŒºåˆ¥ã—ãªã„,ãƒ‰ãƒƒãƒˆã®ç›´å‰ã‚’*ã«ç½®ãæ›ãˆã¦è£œå®Œ
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z} l:|=.'
# è£œå®Œãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ã‚’èª­ã¿ã‚„ã™ãã™ã‚‹
zstyle ':completion:*' verbose yes
zstyle ':completion:*' format '%B%d%b'
zstyle ':completion:*:warnings' format 'No matches for: %d'
zstyle ':completion:*' group-name ''

# syntax highlighting
if [ -f ~/.zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.plugin.zsh ]; then
  source ~/.zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.plugin.zsh
fi

setopt no_complete_aliases

basedir=$(dirname $0)

### alias
source "${basedir}/alias.zsh"

case "${OSTYPE}" in
  darwin*)
    source "${basedir}/mac.zsh"
    ;;
  *)
esac

### cd
setopt auto_cd            # ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªåã ã‘ã§cd
setopt auto_pushd         # å‹æ‰‹ã«push
setopt pushd_ignore_dups  # é‡è¤‡ã—ãŸãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã‚’pushã—ãªã„
function chpwd() { ls }   # ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã‚’ç§»å‹•ã—ãŸã‚‰ls
function name_dir() # dir, name
{
  alias "$2"="cd $1"
}
name_dir ~/dotfiles/ d
name_dir ~/.vim/bundle/ v

### prompt
# git branch
autoload -Uz vcs_info
zstyle ':vcs_info:*' formats '*** branch: %b'
zstyle ':vcs_info:*' actionformats '*** branch: %b / %a'
precmd () {
    psvar=()
    LANG=en_US.UTF-8 vcs_info
    [[ -n "$vcs_info_msg_0_" ]] && psvar[1]="$vcs_info_msg_0_"
}
prompt_git='%B%1(v|%F{cyan}%1v
%f|)'

# default
prompt_exit_status="(%(?.%F{green}%?%f.%F{red}%?%f))"
PROMPT="%B${prompt_git}[%F{cyan}INS%f]${prompt_exit_status} %#%b "
RPROMPT='%B( %F{magenta}%~%f )Oo%b'

# vim keybind mode
function zle-line-init zle-keymap-select {
  local vim_mode='%F{cyan}INS%f'
  if [ "${KEYMAP}" = 'vicmd' ]; then
    vim_mode='%F{red}NOR%f'
  fi
  PROMPT="%B${prompt_git}[${vim_mode}]${prompt_exit_status} %#%b "
  zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

### history
HISTFILE=~/.zsh_history       # ãƒ’ã‚¹ãƒˆãƒªãƒ•ã‚¡ã‚¤ãƒ«
HISTSIZE=1000000
SAVEHIST=1000000
setopt hist_ignore_dups       # é€£ç¶šã—ãŸåŒã˜ã‚³ãƒžãƒ³ãƒ‰ã‚’è¨˜éŒ²ã—ãªã„
setopt hist_ignore_space      # ã‚¹ãƒšãƒ¼ã‚¹ã‹ã‚‰ã¯ã˜ã¾ã‚‹ã‚‚ã®ã‚’ãƒ’ã‚¹ãƒˆãƒªã«ç™»éŒ²ã—ãªã„
setopt extended_history       # ã‚¿ã‚¤ãƒ ã‚¹ã‚¿ãƒ³ãƒ—ã‚’è¨˜éŒ²
setopt share_history
# 5æ–‡å­—ä»¥ä¸Šã®ã‚‚ã®ã®ã¿historyã«ç™»éŒ²
zshaddhistory()
{
  local line=${1%%$'\n'}
  [[ ${#line} -ge 5 ]]
}

command_not_found_handler()
{
  case "${OSTYPE}" in
    darwin*)
      local sed='gsed'
      ;;
    *)
      local sed='sed'
      ;;
  esac
  tail -1 $HISTFILE |
    grep -F "$*" > /dev/null 2>&1 &&
      $sed -i '$d' $HISTFILE
  return 127
}

### keybind
# C-s ã§ã®ã‚µã‚¹ãƒšãƒ³ãƒ‰ã‚’ç„¡åŠ¹
stty stop '' -ixoff

bindkey -v

bindkey "[Z" reverse-menu-complete    # Shift-Tabã§è£œå®Œã‚’é€†é †

autoload history-search-end             # ãƒ’ã‚¹ãƒˆãƒªã‚’å·¡ã‚‹æ™‚ã«ã‚«ãƒ¼ã‚½ãƒ«ã‚’ä¸€ç•ªå¾Œã‚ã«
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey '^P' history-beginning-search-backward-end
bindkey '^N' history-beginning-search-forward-end

bindkey 'OH' beginning-of-line        # Homeã‚­ãƒ¼ãŒã†ã¾ãåŠ¹ã‹ãªã„ã®ã‚’ä¿®æ­£
bindkey 'OF' end-of-line              # Endã‚­ãƒ¼ãŒã†ã¾ãåŠ¹ã‹ãªã„ã®ã‚’ä¿®æ­£

zle -A .backward-kill-word vi-backward-kill-word      # viã‚­ãƒ¼ãƒã‚¤ãƒ³ãƒ‰ã§
zle -A .backward-delete-char vi-backward-delete-char  # ã‚¤ãƒ³ã‚µãƒ¼ãƒˆãƒ¢ãƒ¼ãƒ‰ã«å…¥ã‚‹å‰ã®æ–‡å­—ã‚’æ¶ˆã™

# vicmd mode
bindkey -a ';' execute-named-cmd
bindkey '^A' beginning-of-line
bindkey '^E' end-of-line


### color
# è‰²ã®è¨­å®š
export LSCOLORS=Exfxcxdxbxegedabagacad
# è£œå®Œæ™‚ã®è‰²ã®è¨­å®š
export LS_COLORS='di=01;34:ln=01;35:so=01;32:ex=01;31:bd=46;34:cd=43;34:su=41;30:sg=46;30:tw=42;30:ow=43;30'
# è£œå®Œå€™è£œã«è‰²ã‚’ä»˜ã‘ã‚‹
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}

#ã‚³ãƒžãƒ³ãƒ‰ã‚¹ã‚¿ãƒƒã‚¯
show_buffer_stack() {
  POSTDISPLAY="
  stack: $LBUFFER"
  zle push-line-or-edit
}
zle -N show_buffer_stack
setopt noflowcontrol
bindkey '^Q' show_buffer_stack

### functions
# mkdir ã—ã¦ cd
function take()
{
  if [[ -d $1 ]]; then
    cd $1 && echo -e "\e[1;35m***\e[m\e[1;34m$1\e[m already exists. cd to it"
  else
    mkdir -p $1 && cd $1 && echo -e "\e[1;35m***\e[mCreated \e[1;34m$1\e[m and cd to it"
  fi
}

# ã‚³ãƒžãƒ³ãƒ‰ãƒ©ã‚¤ãƒ³ãŒç©ºã®å ´åˆã€Enterã§è£œå®Œã‚’æ›´æ–°ã™ã‚‹ã€‚
function _rehash()
{
  zle accept-line  # accept-line ãŒãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã®Enterã«å‰²ã‚Šå½“ã¦ã‚‰ã‚Œã¦ã„ã‚‹
  if [[ -z "$BUFFER" ]]; then
    rehash
  fi
}
zle -N _rehash
bindkey "\C-m" _rehash


function is_git_dir()
{
  git rev-parse --show-toplevel > /dev/null 2>&1
}

function /()
{
  if is_git_dir; then
    git grep $@
  else
    grep $@
  fi
}

function f/()
{
  git ls-files G $@
}

# s/$1/$2/g
function sub()
{
  is_git_dir
  local g=$?
  if [ $g != 0 ]; then
    echo 'Current directory is not git directory.'
    return 1
  fi

  local from=$1
  local to=$2

  git grep -l $from | xargs sed -i -e "s/${from}/${to}/g"
}

# $1 is a directory path.
function __new_base()
{
  local dir="$1"
  if [ -e $dir ]; then
    echo "\e[31m$dir already exists.\e[m"
    return
  fi
  mkdir $dir
  cd $dir
  git init && git ci --allow-empty -m 'first commit'
}

# $1 is a project name.
function go-new()
{
  __new_base "$HOME/go/src/github.com/pocke/$1"
}

# $1 is a project name.
function ghq-new()
{
  __new_base "$HOME/ghq/github.com/pocke/$1"
}

# $1 is a project name.
function gem-new()
{
  cd "$HOME/ghq/github.com/pocke/"
  bundle gem $1 &&
    cd $1 &&
    git ci -m 'bunlde gem'
}

### peco
if which peco > /dev/null 2>&1 && [ -e "${basedir}/peco.zsh" ]; then
  source "${basedir}/peco.zsh"
fi

function sideci()
{
  ruby -e '
    spawn(%q!sakura -x "ptmux sideci"!)
    spawn(%q!sakura -x "ptmux node"!)
    spawn(%q!sakura -x "ptmux setaria"!)
    spawn(%q!sakura -x "ptmux catpost"!)
  '
}

function t()
{
  tmux new-session -s $(basename $(pwd) | sed -e 's/\./-/g')
}
